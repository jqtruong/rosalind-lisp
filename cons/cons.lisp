;;;; cons.lisp

(in-package #:cons)

(defvar *sample-dataset* ">Rosalind_1
ATCCAGCT
>Rosalind_2
GGGCAACT
>Rosalind_3
ATGGATCT
>Rosalind_4
AAGCAACC
>Rosalind_5
TTGGAACT
>Rosalind_6
ATGCCATT
>Rosalind_7
ATGGCACT
")

(defvar *sample-output* "ATGCAACT
A: 5 1 0 0 5 5 0 0
C: 0 0 1 4 2 0 6 1
G: 1 1 6 3 0 1 0 0
T: 1 5 0 0 0 1 1 6")

(defun calc-consensus-profile (data)
  "Returns the consensus and its profile calculated from
fasta-formatted data."
  (let* ((fasta-table (funs::make-fasta-hash-table data))
         (lim         (loop for strand being the hash-values in fasta-table
                         minimize (length strand) into lim
                         finally (return lim)))
         (columns     (loop for i below lim
                         collect (loop for strand being the hash-values in fasta-table
                                    collect (char strand i))))
         (profile     (loop for col in columns
                         collect (dna::count-bases (format nil "狺泔飑┅ㄣ镱箦铙躞祜镳骘忉箦泔躅趔轭痱镦殪泔祆邈ㄤ钺汉珏舡磲觑蜷豉忉箦忉箦泔躅趔┅┅鲠祯弩泔铙孱篚痱镦殪濠┅ㄤ彐躅麸篝蜷铉ㄤ狒岍⑶轹孱泔祆邈糸镱镦狒盹篝卑奈篝蜷铉轭嗄猎璃镦羼踽戾铉翳ㄡ盹篝脞皓轭屏釉骘蝽狒义趱蝾泔铙孱篚篝蜷铉犷痱镦殪磲趄轼骘翳泔祆邈糸镱ㄉ箦鲥蜥痫篌殁戾泔铙孱篚篝蜷铉屮轶衄翳孱秕磲蝈趱蝾犷镱镦翳屙眭祠轲戾鲠祯瀛忾钿ㄣ镱箦铙躞痱镦殪濠ㄣ犰悱泔铙孱篚蟓痱镦殪溽翎戾è泔铙孱篚蟓篝蜷铉祜镳骘忉箦泔躅轭泔铙孱篚泔祆邈ㄣ狎忉箦泔躅舂轭麸铘骈钺祆蝈趱蝾ㄦ矧磲铋窿铘螬┅痱镦殪瀛篝蜷铉祜镳骘忉箦泔躅趔轭痱镦殪泔祆邈ㄣ潋ㄡ篌镢＼忉箦泔躅趔┅轭麸馏泔祆邈ㄣ潋ㄡ篌镢＼忉箦泔躅趔┅轭麸皿泔祆邈ㄣ潋ㄡ篌镢＼忉箦泔躅趔┅轭麸求泔祆邈ㄣ潋ㄡ篌镢＼忉箦泔躅趔┅轭麸泽骈钺祆蝈趱蝾ㄦ矧磲铋⒘窿%C: 窿%G: 窿%T: 窿"
                                           As            Cs            Gs            Ts)))))
      (format nil "A%A" consensus-string profile-string))))

;;; Tests
(format t "
;;;;;;;;;;;
;; Tests ;;
;;;;;;;;;;;
Consensus and Profile match: a
"
        (string-equal *sample-output* (to-string *sample-dataset*)))
